# @phlip9's nix packages, home-manager configs, and nixos configs.
{
  #
  # host/target system overridables
  #
  # The system packages will be built on.
  localSystem ? builtins.currentSystem,
  # The system packages will ultimately be run on.
  crossSystem ? localSystem,
  # List of overlay layers used to extend Nixpkgs.
  overlays ? [ ],
  # List of overlays to apply to target packages only.
  crossOverlays ? [ ],
  #
  # pinned sources
  #
  # pinned dependencies, generated by `npins`
  sources ? import ./npins,
  # pinned nixpkgs source path
  nixpkgs ? sources.nixpkgs,
  # pinned old nixpkgs for age-plugin-yubikey that works with older pcscd
  nixpkgs-yubikey ? sources.nixpkgs-yubikey,
  # pinned home-manager source path
  home-manager ? sources.home-manager,
  #
  # package sets
  #
  args ? {
    inherit localSystem overlays;
    config = import ./nix/config-unfree.nix;
  }
  # if we explicitly set crossSystem, even when crossSystem == localSystem,
  # building androidSdk will re-bootstrap since it decides to build
  # glibc-x86_64-unknown-linux-gnu-2.40-66 instead of glibc-2.40-66 for some
  # reason...
  // (
    if crossSystem == localSystem then
      { }
    else
      {
        inherit crossSystem crossOverlays;
      }
  ),
  pkgs ? import nixpkgs args,
  pkgsYubikey ? import nixpkgs-yubikey args,
  hm ? import home-manager { inherit pkgs; },
}:
let
  phlipPkgs = import ./pkgs { inherit pkgs; };
in
rec {
  inherit
    hm
    nixpkgs
    pkgs
    phlipPkgs
    sources
    ;
  lib = pkgs.lib;

  # home-manager configs
  homeConfigurations = {
    phlipdesk = hm.lib.homeManagerConfiguration {
      pkgs = pkgs;
      modules = [ ./home/phlipdesk.nix ];
      extraSpecialArgs = {
        # force hm to use one pkgs eval to reduce eval time by 600ms
        inherit phlipPkgs pkgs;
        inputs = sources;
        pkgsUnfree = pkgs;
        pkgsYubikey = pkgs;
      };
    };
    phliptop-nitro = hm.lib.homeManagerConfiguration {
      pkgs = pkgs;
      modules = [ ./home/phliptop-nitro.nix ];
      extraSpecialArgs = {
        # force hm to use one pkgs eval to reduce eval time by 600ms
        inherit phlipPkgs pkgs pkgsYubikey;
        inputs = sources;
        pkgsUnfree = pkgs;
      };
    };
    phliptop-mbp = hm.lib.homeManagerConfiguration {
      pkgs = pkgs;
      modules = [ ./home/phliptop-mbp.nix ];
      extraSpecialArgs = {
        # force hm to use one pkgs eval to reduce eval time by 600ms
        inherit phlipPkgs pkgs;
        inputs = sources;
        pkgsUnfree = pkgs;
        pkgsYubikey = pkgs;
      };
    };
  };
}
// phlipPkgs
