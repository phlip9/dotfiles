# @phlip9's nix packages, home-manager configs, and nixos configs.
{
  #
  # host/target system overridables
  #
  # The system packages will be built on.
  localSystem ? builtins.currentSystem,
  # The system packages will ultimately be run on.
  crossSystem ? localSystem,
  # List of overlay layers used to extend Nixpkgs.
  overlays ? [ ],
  # List of overlays to apply to target packages only.
  crossOverlays ? [ ],
  #
  # pinned sources
  #
  # pinned dependencies, generated by `npins`
  sources ? import ./npins,
  # pinned nixpkgs source path
  nixpkgs ? sources.nixpkgs,
  # pinned old nixpkgs for age-plugin-yubikey that works with older pcscd
  nixpkgs-yubikey ? sources.nixpkgs-yubikey,
  # pinned home-manager source path
  home-manager ? sources.home-manager,
  #
  # package sets
  #
  args ? (
    {
      inherit localSystem overlays;
      config = import ./nix/config-unfree.nix;
    }
    # if we explicitly set crossSystem, even when crossSystem == localSystem,
    # building androidSdk will re-bootstrap since it decides to build
    # glibc-x86_64-unknown-linux-gnu-2.40-66 instead of glibc-2.40-66 for some
    # reason...
    // (
      if crossSystem == localSystem then
        { }
      else
        { inherit crossSystem crossOverlays; }
    )
  ),
  pkgs ? import nixpkgs args,
  pkgsYubikey ? import nixpkgs-yubikey args,
  hm ? import home-manager { inherit pkgs; },
}:
let
  phlipPkgs = import ./pkgs { inherit pkgs; };
in
{
  inherit
    hm
    phlipPkgs
    pkgs
    sources
    ;

  lib = pkgs.lib;

  # home-manager configs
  homeConfigurations = import ./home {
    inherit
      hm
      phlipPkgs
      pkgs
      pkgsYubikey
      sources
      ;
  };
}
// phlipPkgs
